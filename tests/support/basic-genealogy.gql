define

# Entities

person sub entity,
    plays parent,
    plays child,
    plays spouse,
    plays sibling,
    plays cousin,

    has identifier,
    has firstname,
    has surname,
    has middlename,
    has picture,
    has age,
    has birth-date,
    has death-date,
    has gender;

# Resources

identifier sub attribute, value string;
firstname sub attribute, value string;
surname sub attribute, value string;
middlename sub attribute, value string;
picture sub attribute, value string;
age sub attribute, value long;
birth-date sub attribute, value datetime;
death-date sub attribute, value datetime;
gender sub attribute, value string;

# Roles and Relations

marriage sub relation,
    relates spouse,
    has picture;

spouse sub role;

parentship sub relation,
    relates parent,
    relates child;

parent sub role;
child sub role;

## INFERRED

person
    plays wife,
    plays husband,
    plays granddaughter,
    plays grandson,
    plays father-in-law,
    plays mother-in-law,
    plays daughter-in-law,
    plays son-in-law,
    plays son,
    plays daughter,
    plays grandparent,
    plays grandchild,
    plays child-in-law,
    plays parent-in-law,
    plays mother,
    plays father,
    plays grandmother,
    plays grandfather;

marriage sub relation,
    relates husband,
    relates wife;

husband sub role;
wife sub role;

parentship sub relation,
    relates mother,
    relates father,
    relates son,
    relates daughter;

mother sub parent;
father sub parent;
son sub child;
daughter sub child;

siblings sub relation,
    relates sibling;

sibling sub role;

grandparentship sub relation,
    relates grandparent,
    relates grandfather,
    relates grandmother,
    relates grandchild,
    relates granddaughter,
    relates grandson;

grandparent sub role;
grandfather sub grandparent;
grandmother sub grandparent;
grandchild sub role;
grandson sub grandchild;
granddaughter sub grandchild;

cousins sub relation,
    relates cousin;

cousin sub role;

in-laws sub relation,
    relates child-in-law,
    relates son-in-law,
    relates daughter-in-law,
    relates parent-in-law,
    relates mother-in-law,
    relates father-in-law;

child-in-law sub role;
son-in-law sub child-in-law;
daughter-in-law sub child-in-law;

parent-in-law sub role;
mother-in-law sub parent-in-law;
father-in-law sub parent-in-law;

define

## RULES
#############################################################################
#                                                                           #
#     			Rules to add genderized basic relations		   		        #
#                                                                           #
#############################################################################

genderizeMarriages sub rule,
when { (spouse: $s1, spouse: $s2) isa marriage; $s1 has gender "male"; },
then { (husband: $s1, wife: $s2) isa marriage; };

genderizeMarriages2 sub rule,
when { (spouse: $s1, spouse: $s2) isa marriage; $s1 has gender "female"; },
then { (husband: $s2, wife: $s1) isa marriage; };


genderizeParentships1 sub rule,
when { (parent: $p, child: $c) isa parentship; $p has gender "male"; $c has gender "male"; },
then { (father: $p, son: $c) isa parentship; };

genderizeParentships2 sub rule,
when { (parent: $p, child: $c) isa parentship; $p has gender "male"; $c has gender "female"; },
then { (father: $p, daughter: $c) isa parentship; };

genderizeParentships3 sub rule,
when { (parent: $p, child: $c) isa parentship; $p has gender "female"; $c has gender "male"; },
then { (mother: $p, son: $c) isa parentship; };

genderizeParentships4 sub rule,
when { (parent: $p, child: $c) isa parentship; $p has gender "female"; $c has gender "female"; },
then { (mother: $p, daughter: $c) isa parentship; };


#############################################################################
#                                                                           #
#     				Rules to add inferred  relations		   		        #
#                                                                           #
#############################################################################


grandParents1 sub rule,
when { ($p, son: $gc) isa parentship; (father: $gp, $p) isa parentship; },
then { (grandfather: $gp, grandson: $gc) isa grandparentship; };

grandParents2 sub rule,
when { ($p, daughter: $gc) isa parentship; (father: $gp, $p) isa parentship; },
then { (grandfather: $gp, granddaughter: $gc) isa grandparentship; };

grandParents3 sub rule,
when { ($p, daughter: $gc) isa parentship; (mother: $gp, $p) isa parentship; },
then { (grandmother: $gp, granddaughter: $gc) isa grandparentship; };

grandParents4 sub rule,
when { ($p, son: $gc) isa parentship; (mother: $gp, $p) isa parentship; },
then { (grandmother: $gp, grandson: $gc) isa grandparentship; };


peopleWithSameParentsAreSiblings sub rule,
when { (mother: $m, $x) isa parentship; (mother: $m, $y) isa parentship;
       (father: $f, $x) isa parentship; (father: $f, $y) isa parentship; $x != $y; },
then { (sibling: $x, sibling: $y) isa siblings; };

peopleWithSiblingsParentsAreCousins sub rule,
when { (parent: $p, child: $c1) isa parentship; ($p, $p2) isa siblings; (parent: $p2, child: $c2) isa parentship; },
then { (cousin: $c1, cousin: $c2) isa cousins; };

inLaws1 sub rule,
when { (husband: $x, wife: $y) isa marriage; (mother: $il, $y) isa parentship; },
then { (mother-in-law: $il, son-in-law: $x) isa in-laws; };

inLaws2 sub rule,
when { (husband: $x, wife: $y) isa marriage; (father: $il, $y) isa parentship; },
then { (father-in-law: $il, son-in-law: $x) isa in-laws; };

inLaws3 sub rule,
when { (husband: $x, wife: $y) isa marriage; (father: $il, child: $x) isa parentship; },
then { (father-in-law: $il, daughter-in-law: $y) isa in-laws; };

inLaws4 sub rule,
when { (husband: $x, wife: $y) isa marriage; (mother: $il, child: $x) isa parentship; },
then { (mother-in-law: $il, daughter-in-law: $y) isa in-laws; };
